if (Boolean.valueOf(release) == false) {
    version = version + '-SNAPSHOT'
}
group 'com.github.newnewcoder'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
    }
}
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'maven'

repositories {
    jcenter()
    mavenCentral()
}
ext {
    linebotVersion = '1.6.0'
}
dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "com.linecorp.bot:line-bot-api-client:$linebotVersion"

    testCompile 'junit:junit:4.11'
}

pluginBundle {
    website = 'https://github.com/newnewcoder/linepush'
    vcsUrl = 'https://github.com/newnewcoder/linepush'
    tags = ['linepush']
    description = 'It\'s a gradle plugin using *line-bot message push api* to send line message.'

    plugins {
        linepushPlugin {
            id = "com.github.newnewcoder.linepush"
            displayName = 'Gradle LinePush plugin'
        }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: mavenUrl + 'releases') {
            authentication(userName: repoUser, password: repoPassword)
        }
        snapshotRepository(url: mavenUrl + 'snapshots') {
            authentication(userName: repoUser, password: repoPassword)
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}